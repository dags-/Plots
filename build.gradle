plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.0'
    id 'java'
}

group 'me.dags'
version '1.3.1-SNAPSHOT'
def spongeAPI = '7.1.0'
def spongeChannel = 'SNAPSHOT'

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    mavenCentral()
    maven { url 'http://repo.spongepowered.org/maven' }
    maven { url 'http://maven.sk89q.com/repo' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly "org.spongepowered:spongeapi:${spongeAPI}-$spongeChannel"
    compileOnly ('com.sk89q.worldedit:worldedit-core:6.0.0-SNAPSHOT') {
        transitive = false
    }
    compileOnly ('com.github.TVPT:VoxelSniper:v8.3.0') {
        transitive = false
    }

    shade 'org.mongodb:mongo-java-driver:3.7.0'
    shade 'com.github.dags-:CommandBus:2.0.3'
    shade 'com.github.dags-:NodeData:1.4.1-SNAPSHOT'
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'me.dags.commandbus', 'me.dags.plots.libs.commandbus'
    relocate 'me.dags.data', 'me.dags.plots.libs.data'
    relocate 'com.mongodb', 'me.dags.plots.libs.mongodb'
    relocate 'org.bson', 'me.dags.plots.libs.bson'
    archiveName = "${baseName}-${version}-SpongeAPI-${spongeAPI}.jar"
}

jar.dependsOn shadowJar
jar.enabled = false