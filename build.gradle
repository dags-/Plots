plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id 'java'
}

group 'me.dags'
version '1.2.3-SNAPSHOT'
def spongeAPI = '5.2.0'
def spongeChannel = 'SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'http://repo.spongepowered.org/maven'
    }
    maven {
        url 'http://maven.sk89q.com/repo'
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    compile "org.spongepowered:spongeapi:${spongeAPI}-$spongeChannel"

    compile ('com.github.dags-:PlotsWebAPI:339e5a8') {
        exclude module: 'spongeapi'
    }

    compile ('com.github.TVPT:VoxelSniper:sponge-1.10.2-SNAPSHOT') {
        exclude module: 'spongeapi'
    }

    compile 'com.sk89q.worldedit:worldedit-core:6.0.0-SNAPSHOT'

    // shaded dependencies:
    compile 'org.mongodb:mongo-java-driver:3.2.0'
    compile 'com.github.dags-:CommandBus:1.7.0-RC1'
    compile 'com.github.dags-:Converse:0.1-SNAPSHOT-6'
    compile 'com.github.dags-:NodeData:1.4.1-SNAPSHOT'
}

shadowJar {
    dependencies {
        include dependency('com.github.dags-:NodeData')
        include dependency('com.github.dags-:CommandBus')
        include dependency('org.mongodb:mongo-java-driver')
    }

    relocate 'me.dags.commandbus', 'me.dags.plots.libs.commandbus'
    relocate 'me.dags.data', 'me.dags.plots.libs.data'
    relocate 'com.mongodb', 'me.dags.plots.libs.mongodb'
    relocate 'org.bson', 'me.dags.plots.libs.bson'

    archiveName = "${baseName}-${version}-SpongeAPI-${spongeAPI}.jar"
}

jar.dependsOn shadowJar
jar.enabled = false